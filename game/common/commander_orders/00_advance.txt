advance = {
    texture = "gfx/interface/icons/commander_order_icons/advance.dds"
	
	military_type = army

    possible = {
        AND = {
            error_check = {
                severity = invalid
                has_role = general
            }
        }
    }

    modifier = {
        character_attrition_risk_add = 0.05
        character_advancement_speed_add = 5
		unit_offense_mult = 0.1
    }

	clicksound = "event:/SFX/UI/Military/order_general_front_advance"
	experience = 0.1 # This much extra experience per week while executing Order
	ai_order_type = offensive
}

advance_reckless = {
    texture = "gfx/interface/icons/commander_order_icons/advance.dds"
    
    military_type = army

    possible = {
        AND = {
            error_check = {
                severity = invalid
                has_role = general
            }
            error_check = {
                severity = fail
				has_trait = reckless
            }
        }
    }

    modifier = {
        character_attrition_risk_add = 0.1
        front_advancement_speed_add = 20
        character_advancement_speed_add = 1
        unit_offense_mult = 0.2
        unit_morale_loss_mult = 0.1
        unit_recovery_rate_add = -0.1
        character_battle_condition_aggressive_maneuver_mult = 0.5
        character_battle_condition_ramming_maneuver_mult = 2
        character_battle_condition_blunder_mult = 2
    }

    clicksound = "event:/SFX/UI/Military/order_general_front_advance"
    experience = 0.1 # This much extra experience per week while executing Order
    ai_order_type = offensive
}

advance_planned = {
    texture = "gfx/interface/icons/commander_order_icons/advance.dds"
    
    military_type = army

    possible = {
        AND = {
            error_check = {
                severity = invalid
                has_role = general
            }
            error_check = {
                severity = fail
				OR = {
					has_trait = basic_offensive_planner
					has_trait = experienced_offensive_planner
					has_trait = expert_offensive_planner
				}
            }
        }
    }

    modifier = {
        character_attrition_risk_add = 0.1
        front_advancement_speed_add = 10
        character_advancement_speed_add = 1
        unit_offense_mult = 0.1
        character_battle_condition_surprise_maneuver_mult = 0.5
    }

    clicksound = "event:/SFX/UI/Military/order_general_front_advance"
    experience = 0.1 # This much extra experience per week while executing Order
    ai_order_type = offensive
}

advance_cruel = {
    texture = "gfx/interface/icons/commander_order_icons/advance.dds"
    
    military_type = army

    possible = {
        AND = {
            error_check = {
                severity = invalid
                has_role = general
            }
            error_check = {
                severity = fail
				OR = {
					has_trait = cruel
					has_trait = wrathful
				}
            }
        }
    }

    modifier = {
        character_attrition_risk_add = 0.05
        unit_morale_loss_mult = 0.15
        unit_kill_rate_add = 0.20
        unit_devastation_mult = 0.1
        character_battle_condition_aggressive_maneuver_mult = 1
    }

    clicksound = "event:/SFX/UI/Military/order_general_front_advance"
    experience = 0.1 # This much extra experience per week while executing Order
    ai_order_type = offensive
}

advance_cautious = {
    texture = "gfx/interface/icons/commander_order_icons/advance.dds"
    
    military_type = army

    possible = {
        AND = {
            error_check = {
                severity = invalid
                has_role = general
            }
            error_check = {
                severity = fail
				has_trait = cautious
            }
        }
    }

    modifier = {
        character_attrition_risk_add = -0.1
        front_advancement_speed_add = -10
        character_advancement_speed_add = -0.5
        unit_morale_loss_mult = -0.05
        unit_recovery_rate_add = 0.1
        character_command_limit_mult = -0.2
        character_battle_condition_careful_maneuver_mult = 2
    }

    clicksound = "event:/SFX/UI/Military/order_general_front_advance"
    experience = 0.1 # This much extra experience per week while executing Order
    ai_order_type = offensive
}
